#[==[.rst:
.. cmake:command:: vtksdk_build_modules

  Find, build and wrap VTK modules for VTK-SDK based wheels.

  `package_name` is the name of the python package,
  that will be used to name the folder inside site-packages.
  
  .. code-block:: cmake

  vtksdk_build_modules(<package_name>
    [SOURCE_DIR <path>]
    [ENABLE_TESTS <TRUE_VALUE|FALSE_VALUE>]
    MODULES <module>...
    )
#]==]
function(vtksdk_build_modules package_name)
  cmake_parse_arguments(PARSE_ARGV 0 arg
    ""
    "SOURCE_DIR;ENABLE_TESTS"
    "MODULES"
  )

  # Search for "vtk.module" files in all subdirectories. If not defined, use top level CMakeLists
  if(NOT DEFINED arg_SOURCE_DIR)
    set(arg_SOURCE_DIR ${CMAKE_SOURCE_DIR})
  endif()

  set(_enable_tests OFF)
  if(arg_ENABLE_TESTS)
    set(_enable_tests ON)
  endif()

  if(NOT arg_MODULES)
    message(FATAL_ERROR "MODULES must be defined and a non-empty list of modules.")
  endif()

  include(GNUInstallDirs)

  # Find the VTK-SDK provided VTK package
  # It will be found in a temporary folder generated by scikit-build-core
  # thanks to the [entry-point mecanism](https://github.com/Kitware/vtk-sdk-python-distributions)
  find_package(VTK CONFIG REQUIRED COMPONENTS CommonCore Python)
  # this is a workaround for a VTK-SDK issue on Windows, caused by vtkRenderingOpenXR module.
  # This should later become the only find_package, and it should be REQUIRED!
  # See https://gitlab.kitware.com/vtk/vtk/-/merge_requests/12363 for more info
  find_package(VTK CONFIG)

  set(BUILD_SHARED_LIBS ON) # always build shared, wouldn't make sense not to

  # Fixup RPATHs
  if(APPLE)
    list(APPEND CMAKE_INSTALL_RPATH "@loader_path" "@loader_path/../vtkmodules")
  else() # assumes Linux + GNU-based toolchain
    list(APPEND CMAKE_INSTALL_RPATH "$ORIGIN" "$ORIGIN/../vtkmodules")
    # required as the VTK wheels are build with the C++98 ABI
    list(APPEND CMAKE_CXX_FLAGS "-D_GLIBCXX_USE_CXX11_ABI=0")
  endif ()

  vtk_module_find_modules(_module_files ${arg_SOURCE_DIR})

  # Scan the module files.
  vtk_module_scan(
    MODULE_FILES     ${_module_files}
    REQUEST_MODULES  ${arg_MODULES}
    PROVIDES_MODULES _modules
    ENABLE_TESTS     "${_enable_tests}"
  )

  # Set up the module build.
  vtk_module_build(
    MODULES               ${_modules}
    ARCHIVE_DESTINATION   "${package_name}/lib"
    INSTALL_HEADERS       OFF
    HEADERS_DESTINATION   "${package_name}/include"
    LICENSE_DESTINATION   "${package_name}"
    HIERARCHY_DESTINATION "${package_name}/hierarchy"
    RUNTIME_DESTINATION   "${package_name}"
    LIBRARY_DESTINATION   "${package_name}"
    VERSION                ${SKBUILD_PROJECT_VERSION}
    SOVERSION             "1"
  )

  # Perform the wrapping
  vtk_module_wrap_python(
    MODULES             ${_modules}
    PYTHON_PACKAGE      "${package_name}"
    SOABI               "${Python${VTK_PYTHON_VERSION}_SOABI}"
    BUILD_STATIC        OFF
    BUILD_PYI_FILES     ON
    INSTALL_HEADERS     OFF
    LIBRARY_DESTINATION "." # this generate warnings, but we can't do anything else...
    MODULE_DESTINATION  "."
  )
endfunction()
